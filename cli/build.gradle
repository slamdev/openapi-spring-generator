plugins {
    id 'java-library'
    id 'maven-publish'
    id 'pmd'
    id 'checkstyle'
    id 'com.github.spotbugs' version '4.6.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

dependencies {
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'info.picocli:picocli:4.6.1'
    implementation project(':lib')
}

jar {
    manifest {
        attributes 'Main-Class': 'com.github.slamdev.openapispringgenerator.cli.Application'
    }
}

configurations.archives.artifacts.clear()
artifacts {
    archives tasks.shadowJar
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    minimize {
        exclude dependency('io.swagger.parser.v3:.*:.*')
        exclude dependency('org.slf4j:.*:.*')
    }
    exclude '/arguments/**/*'
    exclude '/mustache/**/*'
    exclude 'META-INF/services/io.swagger.codegen.v3.CodegenConfig'
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/slamdev/openapi-spring-generator")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
